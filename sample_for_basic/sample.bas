1000 CLEAR1500,&HC000:DEFINTA-Z
1010 UA=&HD000:BLOAD"BGMDRV.BIN"
1020 DEFUSR9=UA+&H0 'U=USR9(1)'START BGM DRIVER
1030                'U=USR9(0)'END BGM DRIVER
1040 DEFUSR1=UA+&H3 'U=USR1(&HC000)'PLAY BGM (&HC000=BGM DATA)
1050 DEFUSR2=UA+&H6 'U=USR2(0)'STOP BGM
1060 DEFUSR3=UA+&H9 'U=USR3(&HC800)'PLAY SE (&HC800=SE DATA)
1070 DEFUSR4=UA+&HC 'U=USR4(1)'FADE OUT (WAIT=1)
1080 DEFUSR5=UA+&HF 'U=USR5(0)'IF U<>0 PLAYING NOW
1090 DEFUSR6=UA+&H12'U=USR6(1)'1=PAUSE/0=UNPAUSE
1100 DEFUSR7=UA+&H15'U=USR7(0)'IF U<>0 THEN PAUSE
1110 '
1120 A$="U=USR9(0)":KEY1,A$+CHR$(13)
1130 PRINT"'KEY1,"+CHR$(34)+A$+CHR$(34)+"'END BGM DRIVER":PRINT
1140 ON ERROR GOTO 1370:ON STOP GOSUB 1370:STOP ON
1150 U=USR9(1):PRINT"U=USR9(1)'START BGM DRIVER":PRINT
1160 '
1170 LA=&HC000:BLOAD"BGM02.BIN",LA:U=USR1(LA)'PLAY BGM
1180 '
1190 A=&HC800
1200 S1=A:GOSUB 1410 'SET SE1
1210 U=USR3(S1):PRINT"U=USR3(S1)'play SE1":PRINT
1220 S2=A:GOSUB 1410 'SET SE2
1230 TIME=0:FOR I=0 TO 1:I=-(TIME>60):NEXT
1240 U=USR3(S2):PRINT"U=USR3(S2)'play SE2":PRINT
1250 '
1260 S3=A:GOSUB 1410 'SET SE3
1270 TIME=0:FOR I=0 TO 1:I=-(TIME>60):NEXT
1280 U=USR3(S3):PRINT"U=USR3(S3)'play SE3":PRINT
1290 PRINT "SPACE=SE1 / ENTER=SE2 / ESC=PAUSE / OTHER=END"
1300 I$=INPUT$(1)
1310 IF I$=CHR$(32) THEN U=USR3(S1):GOTO 1300
1320 IF I$=CHR$(13) THEN U=USR3(S2):GOTO 1300
1330 IF I$=CHR$(27) THEN U=USR3(S3):PA=1-PA:U=USR6(PA):GOTO 1300
1340 U=USR4(4)'FADE OUT (WAIT=4)
1350 U=USR5(0):IF U THEN 1350
1360 '
1370 U=USR9(0)'TERMINATE BGM DIRVER
1380 ON ERROR GOTO 0:STOP OFF:END
1390 '
1400 'LOAD SE
1410 READ D:POKE A,D:A=A+1 'PRIORITY
1420 READ A$,D
1430 IF A$="FREQ" THEN POKE A,0:POKE A+1,D AND 255:POKE A+2,PEEK(VARPTR(D)+1):A=A+3:GOTO 1420
1440 IF A$="VOL"  THEN POKE A,1:POKE A+1,D:A=A+2:GOTO 1420
1450 IF A$="NOISE"THEN POKE A,2:POKE A+1,D:A=A+2:GOTO 1420
1460 IF A$="WAIT" THEN POKE A,3:POKE A+1,D:A=A+2:GOTO 1420
1470 IF A$="END"  THEN POKE A,4:A=A+1:RETURN
1480 ERROR 0:POKE A,4:A=A+1:RETURN
1490 '
1500 'SE1
1510 DATA 32 :'PRIORITY
1520 DATA "FREQ",&H8000 :'32768=TONE OFF
1530 DATA "VOL",13,"NOISE",&H80,"WAIT",0
1540 DATA "VOL",12,"NOISE",&H90,"WAIT",0
1550 DATA "VOL",11,"NOISE",&H91,"WAIT",0
1560 DATA "VOL",10,"NOISE",&H92,"WAIT",0
1570 DATA "VOL",9, "NOISE",&H93,"WAIT",0
1580 DATA "VOL",7, "NOISE",&H8A,"WAIT",0
1590 DATA "VOL",6, "NOISE",&H8C,"WAIT",0
1600 DATA "VOL",5, "NOISE",&H8F,"WAIT",0
1610 DATA "VOL",4, "NOISE",&H8C,"WAIT",0
1620 DATA "VOL",3, "NOISE",&H8A,"WAIT",0
1630 DATA "VOL",2, "NOISE",&H88,"WAIT",0
1640 DATA "VOL",1, "NOISE",&H84,"WAIT",0
1650 DATA "END",0
1660 'SE2
1670 DATA 64 :'PRIORITY
1680 DATA "VOL",10,"FREQ",&H6020,"WAIT",0
1690 DATA "VOL",9 ,"FREQ",&H6080,"WAIT",1
1700 DATA "VOL",8 ,"FREQ",&H6070,"WAIT",1
1710 DATA "VOL",7 ,"FREQ",&H6060,"WAIT",1
1720 DATA "VOL",6 ,"FREQ",&H6050,"WAIT",1
1730 DATA "VOL",5 ,"FREQ",&H6040,"WAIT",1
1740 DATA "VOL",4 ,"FREQ",&H6030,"WAIT",1
1750 DATA "VOL",3 ,"FREQ",&H6020,"WAIT",1
1760 DATA "VOL",2 ,"FREQ",&H6010,"WAIT",1
1770 DATA "VOL",1 ,"FREQ",&H6000,"WAIT",1
1780 DATA "END",0
1790 'SE3
1800 DATA 64 :'PRIORITY
1810 DATA "VOL",12,"FREQ",&H6000,"WAIT",0
1820 DATA "VOL",11,"FREQ",&H6010,"WAIT",0
1830 DATA "VOL",10,"FREQ",&H6020,"WAIT",0
1840 DATA "VOL",11,"FREQ",&H6010,"WAIT",0
1850 DATA "VOL",12,"FREQ",&H6000,"WAIT",0
1860 DATA "VOL",11,"FREQ",&H6010,"WAIT",0
1870 DATA "VOL",10,"FREQ",&H6020,"WAIT",0
1880 DATA "VOL",9,"FREQ",&H6080,"WAIT",0
1890 DATA "VOL",8,"FREQ",&H6090,"WAIT",0
1900 DATA "VOL",7,"FREQ",&H60A0,"WAIT",0
1910 DATA "VOL",8,"FREQ",&H6090,"WAIT",0
1920 DATA "VOL",9,"FREQ",&H6080,"WAIT",0
1930 DATA "VOL",8,"FREQ",&H6090,"WAIT",0
1940 DATA "VOL",7,"FREQ",&H60A0,"WAIT",0
1950 DATA "END",0
