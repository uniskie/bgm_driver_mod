1000 CLEAR1500,&HC000:DEFINTA-Z
1010 UA=&HD000:BLOAD"BGMDRV.BIN"
1020 DEFUSR9=UA+&H0'U=USR9(1)'START BGM DRIVER
1030               'U=USR9(0)'END BGM DRIVER
1040 DEFUSR1=UA+&H3'U=USR1(&HC000)'PLAY BGM (&HC000=BGM DATA)
1050 DEFUSR2=UA+&H6'U=USR2(0)'STOP BGM
1060 DEFUSR3=UA+&H9'U=USR3(&HC800)'PLAY SE (&HC800=SE DATA)
1070 DEFUSR4=UA+&HC'U=USR4(1)'FADE OUT (WAIT=1)
1080 DEFUSR5=UA+&HF'U=USR5(0)'IF U<>0 PLAYING NOW
1090 '
1100 A$="U=USR9(0)":KEY1,A$+CHR$(13):PRINT"'KEY1,"+CHR$(34)+A$+CHR$(34)+"'**BGM DRIVER END**":PRINT
1110 ON ERROR GOTO 1250:ON STOP GOSUB 1250:STOP ON
1120 U=USR9(1)'BGM DRIVER START
1130 '
1140 LA=&HC000:BLOAD"BGM02.BIN",LA:U=USR1(LA)'PLAY BGM
1150 '
1160 A=&HC800:S1=A:GOSUB 2010:S2=A:GOSUB 2010
1170 U=USR3(S1):PRINT"U=USR3(S1)'play SE1":PRINT
1180 TIME=0:FOR I=0 TO 1:I=-(TIME>60):NEXT
1190 U=USR3(S2):PRINT"U=USR3(S2)'play SE2":PRINT
1200 '
1210 PRINT "SPACE=SE1 / ENTER=SE2 / OTHER=END"
1220 I$=INPUT$(1)
1230 IF I$=CHR$(32) THEN U=USR3(S1):GOTO 1220
1240 IF I$=CHR$(13) THEN U=USR3(S2):GOTO 1220
1250 U=USR9(0)'TERMINATE BGM DIRVER
1260 ON ERROR GOTO 0:STOP OFF:END
1270 '
2000 'LOAD SE
2010 READ D:POKE A,D:A=A+1 'PRIORITY
2020 READ A$,D
2030 IF A$="FREQ" THEN POKE A,0:POKE A+1,D AND 255:POKE A+2,PEEK(VARPTR(D)+1):A=A+3:GOTO 2020
2040 IF A$="VOL"  THEN POKE A,1:POKE A+1,D:A=A+2:GOTO 2020
2050 IF A$="NOISE"THEN POKE A,2:POKE A+1,D:A=A+2:GOTO 2020
2060 IF A$="WAIT" THEN POKE A,3:POKE A+1,D:A=A+2:GOTO 2020
2070 IF A$="END"  THEN POKE A,4:A=A+1:RETURN
2080 ERROR 0:POKE A,4:A=A+1:RETURN
2090 '
2100 'SE1
2110 DATA 32 :'PRIORITY
2120 DATA "FREQ",&H8000 :'32768=TONE OFF
2130 DATA "VOL",15,"NOISE",&H80,"WAIT",0
2140 DATA "VOL",14,"NOISE",&H81,"WAIT",0
2150 DATA "VOL",13,"NOISE",&H82,"WAIT",0
2160 DATA "VOL",12,"NOISE",&H83,"WAIT",0
2170 DATA "VOL",11,"NOISE",&H86,"WAIT",0
2180 DATA "VOL",10,"NOISE",&H8A,"WAIT",0
2190 DATA "VOL",9, "NOISE",&H8C,"WAIT",0
2200 DATA "VOL",8, "NOISE",&H8F,"WAIT",0
2210 DATA "VOL",7, "NOISE",&H92,"WAIT",0
2220 DATA "VOL",6, "NOISE",&H95,"WAIT",0
2230 DATA "VOL",5, "NOISE",&H99,"WAIT",0
2240 DATA "VOL",4, "NOISE",&H9B,"WAIT",0
2250 DATA "VOL",3, "NOISE",&H8D,"WAIT",0
2260 DATA "VOL",2, "NOISE",&H89,"WAIT",0
2270 DATA "VOL",1, "NOISE",&H87,"WAIT",0
2280 DATA "VOL",0, "NOISE",&H85,"WAIT",0
2290 DATA "END",0
2300 'SE2
2310 DATA 64 :'PRIORITY
2320 DATA "VOL",12,"FREQ",&H6000,"WAIT",0
2330 DATA "VOL",11,"FREQ",&H6010,"WAIT",0
2340 DATA "VOL",10,"FREQ",&H6020,"WAIT",0
2350 DATA "VOL",11,"FREQ",&H6010,"WAIT",0
2360 DATA "VOL",12,"FREQ",&H6000,"WAIT",0
2370 DATA "VOL",11,"FREQ",&H6010,"WAIT",0
2380 DATA "VOL",10,"FREQ",&H6020,"WAIT",0
2390 DATA "VOL",9 ,"FREQ",&H6080,"WAIT",1
2400 DATA "VOL",8 ,"FREQ",&H6070,"WAIT",1
2410 DATA "VOL",7 ,"FREQ",&H6060,"WAIT",1
2420 DATA "VOL",6 ,"FREQ",&H6050,"WAIT",1
2430 DATA "VOL",5 ,"FREQ",&H6040,"WAIT",1
2440 DATA "VOL",4 ,"FREQ",&H6030,"WAIT",1
2450 DATA "VOL",3 ,"FREQ",&H6020,"WAIT",1
2460 DATA "VOL",2 ,"FREQ",&H6010,"WAIT",1
2470 DATA "VOL",1 ,"FREQ",&H6000,"WAIT",1
2480 DATA "END",0
